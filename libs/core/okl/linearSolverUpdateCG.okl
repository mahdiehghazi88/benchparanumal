/*

  The MIT License (MIT)

  Copyright (c) 2017-2022 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

  Permission is hereby granted, free of charge, to any person obtaining a copy
  of this software and associated documentation files (the "Software"), to deal
  in the Software without restriction, including without limitation the rights
  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
  copies of the Software, and to permit persons to whom the Software is
  furnished to do so, subject to the following conditions:

  The above copyright notice and this permission notice shall be included in all
  copies or substantial portions of the Software.

  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
  SOFTWARE.

*/

@kernel void updateCG(const dlong N,
                      const dlong Nblocks,
                      @restrict const dfloat *Ap,
                      const dfloat alpha,
                      @restrict dfloat *r,
                      @restrict dfloat *redr){

  for(dlong b=0;b<Nblocks;++b;@outer(0)){

    @shared dfloat s_dot[AXPYDOT_BLOCKSIZE];

    for(int t=0;t<AXPYDOT_BLOCKSIZE;++t;@inner(0)){
      dlong id = t + b*AXPYDOT_BLOCKSIZE;
      dfloat r_ddot = 0.0;
      while (id<N) {
        const dfloat r_r = r[id] - alpha*Ap[id];
        r[id] = r_r;
        r_ddot += r_r*r_r;
        id += AXPYDOT_BLOCKSIZE*Nblocks;
      }
      s_dot[t] = r_ddot;
    }

#if AXPYDOT_BLOCKSIZE>512
    for(int t=0;t<AXPYDOT_BLOCKSIZE;++t;@inner(0)) if(t<512 && t+512<AXPYDOT_BLOCKSIZE) s_dot[t] += s_dot[t+512];
#endif
#if AXPYDOT_BLOCKSIZE>256
    for(int t=0;t<AXPYDOT_BLOCKSIZE;++t;@inner(0)) if(t<256 && t+256<AXPYDOT_BLOCKSIZE) s_dot[t] += s_dot[t+256];
#endif
    for(int t=0;t<AXPYDOT_BLOCKSIZE;++t;@inner(0)) if(t<128 && t+128<AXPYDOT_BLOCKSIZE) s_dot[t] += s_dot[t+128];
    for(int t=0;t<AXPYDOT_BLOCKSIZE;++t;@inner(0)) if(t< 64 && t+ 64<AXPYDOT_BLOCKSIZE) s_dot[t] += s_dot[t+ 64];
    for(int t=0;t<AXPYDOT_BLOCKSIZE;++t;@inner(0)) if(t< 32 && t+ 32<AXPYDOT_BLOCKSIZE) s_dot[t] += s_dot[t+ 32];
    for(int t=0;t<AXPYDOT_BLOCKSIZE;++t;@inner(0)) if(t< 16 && t+ 16<AXPYDOT_BLOCKSIZE) s_dot[t] += s_dot[t+ 16];
    for(int t=0;t<AXPYDOT_BLOCKSIZE;++t;@inner(0)) if(t<  8 && t+  8<AXPYDOT_BLOCKSIZE) s_dot[t] += s_dot[t+  8];
    for(int t=0;t<AXPYDOT_BLOCKSIZE;++t;@inner(0)) if(t<  4 && t+  4<AXPYDOT_BLOCKSIZE) s_dot[t] += s_dot[t+  4];
    for(int t=0;t<AXPYDOT_BLOCKSIZE;++t;@inner(0)) if(t<  2 && t+  2<AXPYDOT_BLOCKSIZE) s_dot[t] += s_dot[t+  2];
    for(int t=0;t<AXPYDOT_BLOCKSIZE;++t;@inner(0)) if(t<  1 && t+  1<AXPYDOT_BLOCKSIZE) redr[b] = s_dot[0] + s_dot[1];
  }

  for(dlong b=0;b<1;++b;@outer(0)){

    @shared dfloat s_dot[AXPYDOT_BLOCKSIZE];

    for(int t=0;t<AXPYDOT_BLOCKSIZE;++t;@inner(0)){
      dlong id = t;
      dfloat r_ddot = 0.0;
      while (id<Nblocks) {
        r_ddot += redr[id];
        id += AXPYDOT_BLOCKSIZE;
      }
      s_dot[t] = r_ddot;
    }

#if AXPYDOT_BLOCKSIZE>512
    for(int t=0;t<AXPYDOT_BLOCKSIZE;++t;@inner(0)) if(t<512 && t+512<AXPYDOT_BLOCKSIZE) s_dot[t] += s_dot[t+512];
#endif
#if AXPYDOT_BLOCKSIZE>256
    for(int t=0;t<AXPYDOT_BLOCKSIZE;++t;@inner(0)) if(t<256 && t+256<AXPYDOT_BLOCKSIZE) s_dot[t] += s_dot[t+256];
#endif
    for(int t=0;t<AXPYDOT_BLOCKSIZE;++t;@inner(0)) if(t<128 && t+128<AXPYDOT_BLOCKSIZE) s_dot[t] += s_dot[t+128];
    for(int t=0;t<AXPYDOT_BLOCKSIZE;++t;@inner(0)) if(t< 64 && t+ 64<AXPYDOT_BLOCKSIZE) s_dot[t] += s_dot[t+ 64];
    for(int t=0;t<AXPYDOT_BLOCKSIZE;++t;@inner(0)) if(t< 32 && t+ 32<AXPYDOT_BLOCKSIZE) s_dot[t] += s_dot[t+ 32];
    for(int t=0;t<AXPYDOT_BLOCKSIZE;++t;@inner(0)) if(t< 16 && t+ 16<AXPYDOT_BLOCKSIZE) s_dot[t] += s_dot[t+ 16];
    for(int t=0;t<AXPYDOT_BLOCKSIZE;++t;@inner(0)) if(t<  8 && t+  8<AXPYDOT_BLOCKSIZE) s_dot[t] += s_dot[t+  8];
    for(int t=0;t<AXPYDOT_BLOCKSIZE;++t;@inner(0)) if(t<  4 && t+  4<AXPYDOT_BLOCKSIZE) s_dot[t] += s_dot[t+  4];
    for(int t=0;t<AXPYDOT_BLOCKSIZE;++t;@inner(0)) if(t<  2 && t+  2<AXPYDOT_BLOCKSIZE) s_dot[t] += s_dot[t+  2];
    for(int t=0;t<AXPYDOT_BLOCKSIZE;++t;@inner(0)) if(t<  1 && t+  1<AXPYDOT_BLOCKSIZE) redr[b] = s_dot[0] + s_dot[1];
  }
}
