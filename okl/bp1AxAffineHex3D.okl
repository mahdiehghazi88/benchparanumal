/*

The MIT License (MIT)

Copyright (c) 2017-2022 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/

#if KERNEL_NUMBER==0
/*Layer by layer with q in shmem*/

//padding for bank conflicts
#if p_Nq==16
#define p_pad 1
#else
#define p_pad 0
#endif


@kernel void bp1AxAffineHex3D(const dlong Nelements,
                        @restrict const  dlong  *  elementList,
                        @restrict const  dlong  *  GlobalToLocal,
                        @restrict const  dfloat *  wJ,
                        @restrict const  dfloat *  I,
                        @restrict const  dfloat *  MM,
                        @restrict const  dfloat *  q,
                              @restrict dfloat *  Aq){


  for(dlong eo=0; eo<Nelements; eo+=p_NelementsPerBlk; @outer(0)){

    @shared dfloat s_MM[p_Nq][p_Nq+p_pad];
    @shared dfloat s_q[p_Nq][p_NelementsPerBlk][p_Nq][p_Nq+p_pad];

    @exclusive dlong r_e, element;

    for(int es=0;es<p_NelementsPerBlk;++es;@inner(2)){
      for(int j=0;j<p_Nq;++j;@inner(1)){
        for(int i=0;i<p_Nq;++i;@inner(0)){

          if(es==0)
            s_MM[j][i] = MM[i+p_Nq*j];

          r_e = eo + es;

          if(r_e<Nelements) {
            element = elementList[r_e];
            const dfloat J = wJ[element];

            // load pencil of u into register
            const dlong base = i + j*p_Nq + element*p_Np;

            // #pragma unroll p_Nq
            for(int k=0;k<p_Nq;++k) {
              const dlong id = GlobalToLocal[base + k*p_Nq*p_Nq];
              s_q[k][es][j][i] = (id!=-1) ? J*q[id] : 0.0;
            }
          }
        }
      }
    }

    // multiply by M in 'r'
    for(int es=0;es<p_NelementsPerBlk;++es;@inner(2)){
      for(int k=0;k<p_Nq;++k;@inner(1)){
        for(int j=0;j<p_Nq;++j;@inner(0)){

          dfloat r_q[p_Nq];

          // #pragma unroll p_Nq
          for(int i=0;i<p_Nq;++i)
            r_q[i] = s_q[k][es][j][i];

          // #pragma unroll p_Nq
          for(int i=0;i<p_Nq;++i){

            dfloat Mq = 0;

            // #pragma unroll p_Nq
            for(int m=0;m<p_Nq;++m){
              Mq += s_MM[m][i]*r_q[m];
            }

            s_q[k][es][j][i] = Mq; // ok since only this thread
          }
        }
      }
    }

    // multiply by M in 's'
    for(int es=0;es<p_NelementsPerBlk;++es;@inner(2)){
      for(int k=0;k<p_Nq;++k;@inner(1)){
        for(int i=0;i<p_Nq;++i;@inner(0)){

          dfloat r_q[p_Nq];

          // #pragma unroll p_Nq
          for(int j=0;j<p_Nq;++j)
            r_q[j] = s_q[k][es][j][i];

          // #pragma unroll p_Nq
          for(int j=0;j<p_Nq;++j){

            dfloat Mq = 0;

            // #pragma unroll p_Nq
            for(int m=0;m<p_Nq;++m){
              Mq += s_MM[m][j]*r_q[m];
            }

            s_q[k][es][j][i] = Mq; // ok since only this thread
          }
        }
      }
    }

    // multiply by M in 't'
    for(int es=0;es<p_NelementsPerBlk;++es;@inner(2)){
      for(int j=0;j<p_Nq;++j;@inner(1)){
        for(int i=0;i<p_Nq;++i;@inner(0)){

          if (r_e<Nelements) {

            dfloat r_q[p_Nq];

            // #pragma unroll p_Nq
            for(int k=0;k<p_Nq;++k)
              r_q[k] = s_q[k][es][j][i];

            // #pragma unroll p_Nq
            for(int k=0;k<p_Nq;++k){

              dfloat Mq = 0;

              // #pragma unroll p_Nq
              for(int m=0;m<p_Nq;++m){
                Mq += s_MM[m][k]*r_q[m];
              }

              Aq[element*p_Np+k*p_Nq*p_Nq+j*p_Nq+i] = Mq;
            }
          }
        }
      }
    }
  }
}
#endif


#if KERNEL_NUMBER==1
/*Layer by layer with q in regs*/

//padding for bank conflicts
#if p_Nq==16
#define p_pad 1
#else
#define p_pad 0
#endif


@kernel void bp1AxAffineHex3D(const dlong Nelements,
                        @restrict const  dlong  *  elementList,
                        @restrict const  dlong  *  GlobalToLocal,
                        @restrict const  dfloat *  wJ,
                        @restrict const  dfloat *  I,
                        @restrict const  dfloat *  MM,
                        @restrict const  dfloat *  q,
                              @restrict dfloat *  Aq){


  for(dlong eo=0; eo<Nelements; eo+=p_NelementsPerBlk; @outer(0)){

    @shared dfloat s_MM[p_Nq][p_Nq+p_pad];
    @shared dfloat s_q[p_NelementsPerBlk][p_Nq][p_Nq+p_pad];

    @exclusive dlong element;

    // register array to hold q(i,j,0:N) private to thread
    @exclusive dfloat r_q[p_Nq];
    // array for results Mq(i,j,0:N)
    @exclusive dfloat r_Mq[p_Nq];

    for(int es=0;es<p_NelementsPerBlk;++es;@inner(2)){
      for(int j=0;j<p_Nq;++j;@inner(1)){
        for(int i=0;i<p_Nq;++i;@inner(0)){

          if(es==0) s_MM[j][i] = MM[i+p_Nq*j];

          const dlong e = eo + es;

          if(e<Nelements) {
            element = elementList[e];
            const dfloat J = wJ[element];

            // load pencil of u into register
            const dlong base = i + j*p_Nq + element*p_Np;

            // #pragma unroll p_Nq
            for(int k=0;k<p_Nq;++k) {
              const dlong id = GlobalToLocal[base + k*p_Nq*p_Nq];
              r_q[k] = (id!=-1) ? J*q[id] : 0.0;
            }
          } else {
            element = -1;
          }

          for (int k=0;k<p_Nq;k++) {
            r_Mq[k] = 0.0;
          }
        }
      }
    }

    // Layer by layer
    for(int k=0; k<p_Nq; k++){

      for(int es=0;es<p_NelementsPerBlk;++es;@inner(2)){
        for(int j=0;j<p_Nq;++j;@inner(1)){
          for(int i=0;i<p_Nq;++i;@inner(0)){
            // load a layer of q into shmem
            s_q[es][j][i] = r_q[k];
          }
        }
      }

      @exclusive dfloat Mq;

      // multiply by M in 'r'
      for(int es=0;es<p_NelementsPerBlk;++es;@inner(2)){
        for(int j=0;j<p_Nq;++j;@inner(1)){
          for(int i=0;i<p_Nq;++i;@inner(0)){

            Mq = 0;
            for (int m=0;m<p_Nq;m++) {
              Mq += s_MM[i][m]*s_q[es][j][m];
            }
          }
        }
      }

      for(int es=0;es<p_NelementsPerBlk;++es;@inner(2)){
        for(int j=0;j<p_Nq;++j;@inner(1)){
          for(int i=0;i<p_Nq;++i;@inner(0)){
            s_q[es][j][i] = Mq;
          }
        }
      }

      // multiply by M in 's'
      for(int es=0;es<p_NelementsPerBlk;++es;@inner(2)){
        for(int j=0;j<p_Nq;++j;@inner(1)){
          for(int i=0;i<p_Nq;++i;@inner(0)){

            Mq = 0;

            for (int m=0;m<p_Nq;m++) {
              Mq += s_MM[j][m]*s_q[es][m][i];
            }

            for (int m=0;m<p_Nq;m++) {
              r_Mq[m] += s_MM[k][m]*Mq;
            }
          }
        }
      }
    }

    for(int es=0;es<p_NelementsPerBlk;++es;@inner(2)){
      for(int j=0;j<p_Nq;++j;@inner(1)){
        for(int i=0;i<p_Nq;++i;@inner(0)){

          if (element>-1) {
            const dlong base = i + j*p_Nq + element*p_Np;

            for(int k=0;k<p_Nq;++k){
              Aq[base+k*p_Nq*p_Nq] = r_Mq[k];
            }
          }
        }
      }
    }
  }
}
#endif

#if KERNEL_NUMBER==2
/*MFMA 4x4, layer by layer with q in regs*/
#define p_pad 1

@kernel void bp1AxAffineHex3D(const dlong Nelements,
                        @restrict const  dlong  *  elementList,
                        @restrict const  dlong  *  GlobalToLocal,
                        @restrict const  dfloat *  wJ,
                        @restrict const  dfloat *  I,
                        @restrict const  dfloat *  MM,
                        @restrict const  dfloat *  q,
                              @restrict dfloat *  Aq){


  for(dlong e=0; e<Nelements; e++; @outer(0)){

    @shared dfloat s_MM[16][16+p_pad];
    @shared dfloat s_q[16][16+p_pad];

    @exclusive dlong element;

    // register array to hold q(i,j,0:N) private to thread
    @exclusive dfloat r_q[p_Nq];
    // array for results Mq(i,j,0:N)
    @exclusive dfloat r_Mq[p_Nq];

    for(int j=0;j<16;++j;@inner(1)){
      for(int i=0;i<16;++i;@inner(0)){

        s_MM[j][i] = (i<p_Nq && j<p_Nq) ? MM[i+p_Nq*j] : 0.0;

        element = elementList[e];
        const dfloat J = wJ[element];

        // load pencil of q into register
        const dlong base = i + j*p_Nq + element*p_Np;

        // #pragma unroll p_Nq
        for(int k=0;k<p_Nq;++k) {
          const dlong id = (i<p_Nq && j<p_Nq) ? GlobalToLocal[base + k*p_Nq*p_Nq] : -1;
          r_q[k] = (id!=-1) ? J*q[id] : 0.0;
        }

        for (int k=0;k<p_Nq;k++) {
          r_Mq[k] = 0.0;
        }
      }
    }

    // Layer by layer
    for(int k=0; k<p_Nq; k++){

      for(int j=0;j<16;++j;@inner(1)){
        for(int i=0;i<16;++i;@inner(0)){
          // load a layer of q into shmem
          s_q[j][i] = r_q[k];
        }
      }

      @exclusive dfloat Mq;

      // multiply by M in 'r'
      for(int j=0;j<16;++j;@inner(1)){
        for(int i=0;i<16;++i;@inner(0)){

          Mq = 0;

          for (int m=0;m<p_Nq;m+=4) {
            const dfloat r_M = s_MM[i][m+(j%4)]; //replicated 16 columns and 4 rows of M in each wave
            const dfloat r_q = s_q[(i%4)+4*(j/4)][m+(j%4)]; //replicated 4x4 matrix of dofs
            Mq = __builtin_amdgcn_mfma_f64_4x4x4f64(r_q, r_M, Mq, 0, 0, 0);
          }
        }
      }

      for(int j=0;j<16;++j;@inner(1)){
        for(int i=0;i<16;++i;@inner(0)){
          s_q[j][i] = Mq;
        }
      }

      // multiply by M in 's'
      for(int j=0;j<16;++j;@inner(1)){
        for(int i=0;i<16;++i;@inner(0)){

          Mq = 0;

          for (int m=0;m<p_Nq;m+=4) {
            const dfloat r_M = s_MM[i%4+4*(j/4)][m+j%4];//replicated 4x4 submatrix of M
            const dfloat r_q = s_q[m+j%4][i]; //replicated 16 x-slices and 4 y-slices of q in each wave
            Mq = __builtin_amdgcn_mfma_f64_4x4x4f64(r_M, r_q, Mq, 0, 0, 0);
          }

          for (int m=0;m<p_Nq;m++) {
            r_Mq[m] += s_MM[k][m]*Mq;
          }
        }
      }
    }

    for(int j=0;j<16;++j;@inner(1)){
      for(int i=0;i<16;++i;@inner(0)){
        if(i<p_Nq && j<p_Nq){
          const dlong base = i + j*p_Nq + element*p_Np;

          for(int k=0;k<p_Nq;++k){
            Aq[base+k*p_Nq*p_Nq] = r_Mq[k];
          }
        }
      }
    }
  }
}
#endif
