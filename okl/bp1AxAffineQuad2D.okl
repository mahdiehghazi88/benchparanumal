/*

The MIT License (MIT)

Copyright (c) 2017-2022 Tim Warburton, Noel Chalmers, Jesse Chan, Ali Karakus

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

*/
#if KERNEL_NUMBER==0
@kernel void bp1AxAffineQuad2D(const dlong Nelements,
                        @restrict const  dlong  *  elementList,
                        @restrict const  dlong  *  GlobalToLocal,
                        @restrict const  dfloat *  wJ,
                        @restrict const  dfloat *  I,
                        @restrict const  dfloat *  MM,
                        @restrict const  dfloat *  q,
                              @restrict dfloat *  Aq){

  for(int eo=0;eo<Nelements;eo+=p_NelementsPerBlk*p_NelementsPerThd;@outer(0)){

    @shared dfloat s_MM[p_Nq][p_Nq];
    @shared dfloat  s_q[p_NelementsPerBlk][p_NelementsPerThd][p_Nq][p_Nq];

    @exclusive dlong element[p_NelementsPerThd];

    for(int es=0;es<p_NelementsPerBlk;++es;@inner(2)){
      for(int j=0;j<p_Nq;++j;@inner(1)){
        for(int i=0;i<p_Nq;++i;@inner(0)){

          //load operators
          if(es==0){
            const int id = j*p_Nq+i;
            const dfloat MMji = MM[id];
            s_MM[j][i] = MMji;
          }

          for (int ek=0;ek<p_NelementsPerThd;++ek) {
            if(eo+es*p_NelementsPerThd+ek<Nelements) {
              element[ek] = elementList[eo+es*p_NelementsPerThd+ek];
              const dfloat J = wJ[element[ek]];
              const dlong id = GlobalToLocal[i + j*p_Nq + element[ek]*p_Np];
              s_q[es][ek][j][i] = (id!=-1) ? J*q[id] : 0.0;
            }
          }
        }
      }
    }

    // multiply by M in 'r'
    @exclusive dfloat Mq[p_NelementsPerThd];

    for(int es=0;es<p_NelementsPerBlk;++es;@inner(2)){
      for(int j=0;j<p_Nq;++j;@inner(1)){
        for(int i=0;i<p_Nq;++i;@inner(0)){

          for(int s=0;s<p_NelementsPerThd;++s)
            Mq[s] = 0;

          // #pragma unroll p_Nq
          for(int m=0;m<p_Nq;++m){
            dfloat Mim = s_MM[m][i];

            // #pragma unroll p_NelementsPerThd
            for(int s=0;s<p_NelementsPerThd;++s){
              Mq[s] += Mim*s_q[es][s][j][m];
            }
          }
        }
      }
    }

    for(int es=0;es<p_NelementsPerBlk;++es;@inner(2)){
      for(int j=0;j<p_Nq;++j;@inner(1)){
        for(int i=0;i<p_Nq;++i;@inner(0)){
          for(int s=0;s<p_NelementsPerThd;++s){
            s_q[es][s][j][i] = Mq[s];
          }
        }
      }
    }

    // multiply by M in 's'
    for(int es=0;es<p_NelementsPerBlk;++es;@inner(2)){
      for(int j=0;j<p_Nq;++j;@inner(1)){
        for(int i=0;i<p_Nq;++i;@inner(0)){

          for(int s=0;s<p_NelementsPerThd;++s)
            Mq[s] = 0;

          // #pragma unroll p_Nq
          for(int m=0;m<p_Nq;++m){
            dfloat Mim = s_MM[m][j];

            // #pragma unroll p_NelementsPerThd
            for(int s=0;s<p_NelementsPerThd;++s){
              Mq[s] += Mim*s_q[es][s][m][i];
            }
          }

          // #pragma unroll p_NelementsPerThd
          for(int ek=0;ek<p_NelementsPerThd;++ek){
            if(eo+es*p_NelementsPerThd+ek<Nelements){
              const dlong base = i + j*p_Nq + element[ek]*p_Np;
              Aq[base] = Mq[ek];
            }
          }
        }
      }
    }
  }
}
#endif

#if KERNEL_NUMBER==1
/*MFMA 4x4*/

//padding for bank conflicts
#define p_pad 1

@kernel void bp1AxAffineQuad2D(const dlong Nelements,
                        @restrict const  dlong  *  elementList,
                        @restrict const  dlong  *  GlobalToLocal,
                        @restrict const  dfloat *  wJ,
                        @restrict const  dfloat *  I,
                        @restrict const  dfloat *  MM,
                        @restrict const  dfloat *  q,
                              @restrict dfloat *  Aq){

  for(dlong eo=0; eo<Nelements; eo+=p_NelementsPerThd; @outer(0)){

    @shared dfloat s_MM[16][16+p_pad];
    @shared dfloat  s_q[p_NelementsPerThd][16][16+p_pad];

    @exclusive dlong element[p_NelementsPerThd];

    for(int j=0;j<16;++j;@inner(1)){
      for(int i=0;i<16;++i;@inner(0)){

        //load operator
        s_MM[j][i] = (i<p_Nq && j<p_Nq) ? MM[p_Nq*j+i] : 0.0;

        for (int et=0;et<p_NelementsPerThd;++et) {
          const dlong e = eo + et;
          if(e<Nelements) {
            element[et] = elementList[e];
            const dfloat J = wJ[element[et]];

            const dlong id = (i<p_Nq && j<p_Nq) ? GlobalToLocal[i + j*p_Nq + element[et]*p_Np] : -1;
            s_q[et][j][i] = (id!=-1) ? J*q[id] : 0.0;
          } else {
            element[et] = -1;
            s_q[et][j][i] = 0.0;
          }
        }
      }
    }

    // multiply by M in 'r'
    @exclusive dfloat Mq[p_NelementsPerThd];

    for(int j=0;j<16;++j;@inner(1)){
      for(int i=0;i<16;++i;@inner(0)){

        for (int et=0;et<p_NelementsPerThd;++et) {
          Mq[et] = 0;
        }

        for (int m=0;m<p_Nq;m+=4) {
          const dfloat r_M = s_MM[i][m+(j%4)]; //replicated 16 columns and 4 rows of M in each wave

          for (int et=0;et<p_NelementsPerThd;++et) {
            const dfloat r_q = s_q[et][(i%4)+4*(j/4)][m+(j%4)]; //replicated 4x4 matrix of dofs
            Mq[et] = __builtin_amdgcn_mfma_f64_4x4x4f64(r_q, r_M, Mq[et], 0, 0, 0);
          }
        }
      }
    }

    for(int j=0;j<16;++j;@inner(1)){
      for(int i=0;i<16;++i;@inner(0)){
        for (int et=0;et<p_NelementsPerThd;++et) {
          s_q[et][j][i] = Mq[et];
        }
      }
    }

    // multiply by M in 's'
    for(int j=0;j<16;++j;@inner(1)){
      for(int i=0;i<16;++i;@inner(0)){

        for (int et=0;et<p_NelementsPerThd;++et) {
          Mq[et] = 0;
        }

        for (int m=0;m<p_Nq;m+=4) {
          const dfloat r_M = s_MM[i%4+4*(j/4)][m+j%4];//replicated 4x4 submatrix of M

          for (int et=0;et<p_NelementsPerThd;++et) {
            const dfloat r_q = s_q[et][m+j%4][i]; //replicated 16 x-slices and 4 y-slices of q in each wave
            Mq[et] = __builtin_amdgcn_mfma_f64_4x4x4f64(r_M, r_q, Mq[et], 0, 0, 0);
          }
        }

        if(i<p_Nq && j<p_Nq){
          for (int et=0;et<p_NelementsPerThd;++et) {
            if (element[et]>-1) {
              const dlong base = i + j*p_Nq + element[et]*p_Np;
              Aq[base] = Mq[et];
            }
          }
        }
      }
    }
  }
}
#endif
